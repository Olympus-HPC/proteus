@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
set(PROTEUS_LLVM_DIR "@LLVM_DIR@")
set(PROTEUS_CLANG_DIR "@Clang_DIR@")

if(DEFINED LLVM_DIR)
    file(REAL_PATH "${LLVM_DIR}" LLVM_DIR)
    if (NOT "${LLVM_DIR}" STREQUAL "${PROTEUS_LLVM_DIR}")
        message(FATAL_ERROR "Mismatch between target LLVM_DIR = ${LLVM_DIR} "
        "and Proteus LLVM_DIR = ${PROTEUS_LLVM_DIR}. "
        "Use the same LLVM installation when compiling.")
    endif()
else()
    find_dependency(LLVM REQUIRED CONFIG HINTS "${PROTEUS_LLVM_DIR}" NO_DEFAULT_PATH)
endif()

if(DEFINED Clang_DIR)
    file(REAL_PATH "${Clang_DIR}" Clang_DIR)
    if (NOT "${Clang_DIR}" STREQUAL "${PROTEUS_CLANG_DIR}")
        message(FATAL_ERROR "Mismatch between target Clang_DIR = ${Clang_DIR} "
        "and Proteus Clang_DIR = ${PROTEUS_CLANG_DIR}. "
        "Use the same LLVM installation when compiling.")
    endif()
else()
    find_dependency(Clang REQUIRED CONFIG HINTS "${PROTEUS_CLANG_DIR}" NO_DEFAULT_PATH)
endif()

# We want to link with LLD with HIP.
set(PROTEUS_ENABLE_HIP "@PROTEUS_ENABLE_HIP@")
if(PROTEUS_ENABLE_HIP)
    set(PROTEUS_LLD_DIR "@LLD_DIR@")

    if(DEFINED LLD_DIR)
        file(REAL_PATH "${LLD_DIR}" LLD_DIR)
        if (NOT "${LLD_DIR}" STREQUAL "${PROTEUS_LLD_DIR}")
            message(FATAL_ERROR "Mismatch between target LLD_DIR = ${LLD_DIR} "
            "and Proteus LLD_DIR = ${PROTEUS_LLD_DIR}. "
            "Use the same LLVM installation when compiling.")
        endif()
    else()
        find_dependency(LLD REQUIRED CONFIG HINTS "${PROTEUS_LLD_DIR}" NO_DEFAULT_PATH)
    endif()
endif()

message(STATUS "LLVM DIR = ${LLVM_DIR}")

include("${CMAKE_CURRENT_LIST_DIR}/ProteusFunctions.cmake")
include("${CMAKE_CURRENT_LIST_DIR}/proteusTargets.cmake")
check_required_components("@PROJECT_NAME@")
