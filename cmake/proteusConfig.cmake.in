@PACKAGE_INIT@

set(PROTEUS_LLVM_DIR "@LLVM_DIR@")
set(PROTEUS_LLD_DIR "@LLD_DIR@")
find_dependency(LLVM REQUIRED CONFIG)

if (NOT "${LLVM_DIR}" STREQUAL "${PROTEUS_LLVM_DIR}")
    message(FATAL_ERROR "Mismatch between target LLVM_DIR = ${LLVM_DIR} "
    "and Proteus LLVM_DIR = ${PROTEUS_LLVM_DIR}. "
    "Use the same LLVM installation when compiling.")
endif()
message(STATUS "LLVM DIR = ${LLVM_DIR}")

get_property(enabled_languages GLOBAL PROPERTY ENABLED_LANGUAGES)
message(STATUS "Enabled languages: ${enabled_languages}")
if("HIP" IN_LIST enabled_languages)
    find_dependency(LLD NO_DEFAULT_PATH PATHS ${PROTEUS_LLD_DIR} REQUIRED)
    message(STATUS "LLD DIR = ${LLD_DIR}")
endif()

include("${CMAKE_CURRENT_LIST_DIR}/proteusTargets.cmake")
check_required_components("@PROJECT_NAME@")

function(add_proteus target)
    target_compile_options(${target} PUBLIC
        "-fpass-plugin=\$<TARGET_FILE:ProteusPass>"
    )

    target_link_options(${target} PUBLIC
        "SHELL:\$<\$<LINK_LANGUAGE:HIP>:-Xoffload-linker --load-pass-plugin=\$<TARGET_FILE:ProteusPass>>")

    target_link_libraries(${target} PUBLIC proteus)
endfunction()
